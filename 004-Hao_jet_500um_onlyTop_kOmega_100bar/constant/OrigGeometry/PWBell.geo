// Gmsh project created on Fri Jul 12 21:12:05 2024
SetFactory("OpenCASCADE");

// Input
//---------------------------------------

Mesh_Start = 1;             // 0: Dont Mesh, 1: Mesh
Mesh_Elements_Order = 1;    // 1: P1, 2: P2

// Geometry
//---------------------------------------
//+
// parameters

sb = 0.5; // side length of box in mm
rb = sb / 2;

rt = 0.025; // throat radius
re = 0.05; // exit radius
dxj = 0.005; // 5um resolution at gas jet

sjt = 0.12; // side length of plate at top of jet for jet hole
rjt = sjt/2;
// All coordinates are in mm

// points

// bottom of gas cell
Point(1) = {-rb, -rb, 0, rb/10};
Point(2) = {rb, -rb, 0, rb/10};
Point(3) = {rb, rb, 0, rb/10};
Point(4) = {-rb, rb, 0, rb/10};

// top of gas cell
Point(5) = {-rb, -rb, rb, rb/10};
Point(6) = {rb, -rb, rb, rb/10};
Point(7) = {rb, rb, rb, rb/10};
Point(8) = {-rb, rb, rb, rb/10};

// gas jet exit
Point(9) = {0, 0, 0, dxj}; // center
Point(10) = {-re, 0, 0, dxj}; // 50um radius at exit
Point(11) = {0, -re, 0, dxj};
Point(12) = {re, 0, 0, dxj};
Point(13) = {0, re, 0, dxj};

// gas jet throat
// The length of the diverging section is chosen to be 69 um (nice) to match the mach angle
// that is generated by a 50 um throat and 100 um exit
Point(14) = {0, 0, -0.069, dxj}; // center
Point(15) = {-rt, 0, -0.069, dxj}; // 25um radius at throat
Point(16) = {0, -rt, -0.069, dxj};
Point(17) = {rt, 0, -0.069, dxj};
Point(18) = {0, rt, -0.069, dxj};

// gas jet inlet, let's also make this section 69 um long arbitrarily
Point(19) = {0, 0, -0.138, dxj}; // center
Point(20) = {-re, 0, -0.138, dxj}; // 25um radius at throat
Point(21) = {0, -re, -0.138, dxj};
Point(22) = {re, 0, -0.138, dxj};
Point(23) = {0, re, -0.138, dxj};

// lines
// gas cell
Line(1) = {1, 2};
Line(2) = {2, 3};
Line(3) = {3, 4};
Line(4) = {4, 1};

Line(5) = {5, 6};
Line(6) = {6, 7};
Line(7) = {7, 8};
Line(8) = {8, 5};

Line(9) = {1, 5};
Line(10) = {2, 6};
Line(11) = {3, 7};
Line(12) = {4, 8};

// gas jet
Circle(13) = {10, 9, 11};
Circle(14) = {11, 9, 12};
Circle(15) = {12, 9, 13};
Circle(16) = {13, 9, 10};

Circle(17) = {15, 14, 16};
Circle(18) = {16, 14, 17};
Circle(19) = {17, 14, 18};
Circle(20) = {18, 14, 15};

Circle(21) = {20, 19, 21};
Circle(22) = {21, 19, 22};
Circle(23) = {22, 19, 23};
Circle(24) = {23, 19, 20};

Line(25) = {10, 15};
Line(26) = {11, 16};
Line(27) = {12, 17};
Line(28) = {13, 18};

Line(29) = {15, 20};
Line(30) = {16, 21};
Line(31) = {17, 22};
Line(32) = {18, 23};

// curve loops
Curve Loop(1) = {1, 2, 3, 4};
Curve Loop(2) = {5, 6, 7, 8};
Curve Loop(3) = {1, 10, 5, 9};
Curve Loop(4) = {2, 11, 6, 10};
Curve Loop(5) = {3, 12, 7, 11};
Curve Loop(6) = {4, 9, 8, 12};

Curve Loop(7) = {13:16};
Curve Loop(8) = {21:24};

Curve Loop(9) = {13, 26, 17, 25};
Curve Loop(10) = {14, 27, 18, 26};
Curve Loop(11) = {15, 28, 19, 27};
Curve Loop(12) = {16, 25, 20, 28};

Curve Loop(13) = {17, 30, 21, 29};
Curve Loop(14) = {18, 31, 22, 30};
Curve Loop(15) = {19, 32, 23, 31};
Curve Loop(16) = {20, 29, 24, 32};

// surfaces
// surfaces have same label as curve loops, EXCEPT surface 1,
// which consists of curve loop 1 and 7 (it's the bottom of the gas cell)
// (There will be no surface 7)

// gas cell

Plane Surface(1) = {1, 7};
Plane Surface(2) = {2};
Plane Surface(3) = {3};
Plane Surface(4) = {4};
Plane Surface(5) = {5};
Plane Surface(6) = {6};

Plane Surface(8) = {8}; //inlet

Surface(9) = {9};
Surface(10) = {10};
Surface(11) = {11};
Surface(12) = {12};
Surface(13) = {13};
Surface(14) = {14};
Surface(15) = {15};
Surface(16) = {16};

Surface Loop(1) = {1:6, 8:16};

// volumes
Volume(1) = {1};

// Physical surfaces and volumes
Physical Surface("inlet", 1) = {8};
Physical Surface("pump", 2) = {2:6};
Physical Surface("wall", 3) = {1, 9:16};
Physical Volume("internal", 4) = {1};

// do mesh 

If (Mesh_Start)

  Mesh.MshFileVersion = 2.2;
  Mesh.SaveAll = 0;
  Mesh.SaveElementTagType = 2;
  Mesh.ElementOrder = 1; // 1: P1, 2: P2
  Mesh.Algorithm = 8;
  Mesh.Algorithm3D = 10;
  Mesh.RecombinationAlgorithm = 2;
  Mesh 3;
  Mesh.Recombine3DAll = 0;
  Mesh.Recombine3DLevel = 0;
  

  Save "PWBell.msh";
  Save "PWBell.vtk";

  

EndIf


